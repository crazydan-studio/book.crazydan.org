[#chp_6]
== Chapter 6. Control Structures

This chapter describes _Chez Scheme_ extensions to the set of standard control structures. See link:../../the-scheme-programming-language-4th/en/index.html#chp_control_operations[Chapter 5] of _The Scheme Programming Language, 4th Edition_ or the Revised^6^ Report on Scheme for a description of standard control structures.

=== Section 6.1. Conditionals [[section_6.1.]]

[horizontal]
syntax:: `(exclusive-cond _clause~1~_ _clause~2~_ \...)`
returns:: see below
libraries:: `(chezscheme)`

`exclusive-cond` is a version of `cond` (<<section_5.3.,Section 5.3>> of TSPLFOUR) that differs from `cond` in that the tests embedded within the clauses are assumed to be exclusive in the sense that if one of the tests is true, the others are not. This allows the implementation to reorder clauses when profiling information is available at expansion time (<<section_12.7.,Section 12.7>>).

The `(_test_)` form of clause is not supported. The order chosen when profiling information is available is based on the relative numbers of times the RHS of each clause is executed, and `(_test_)` has no RHS. `(_test_ \=> values)` is equivalent, abeit less concise.

[horizontal]
syntax:: `(case _expr~0~_ _clause~1~_ _clause~2~_ \...)`
returns:: see below
libraries:: `(chezscheme)`

Each clause but the last must take one of the forms:

[source,scheme,subs="quotes"]
----
((_key_ ...) _expr~1~_ _expr~2~_ ...)
(_key_ _expr~1~_ _expr~2~_ ...)
----

where each `_key_` is a datum distinct from the other keys. The last clause may be in the above form or it may be an `else` clause of the form

[source,scheme,subs="quotes"]
----
(else _expr~1~_ _expr~2~_ ...)
----

`_expr~0~_` is evaluated and the result is compared (using `equal?`) against the keys of each clause in order. If a clause containing a matching key is found, the expressions `_expr~1~_ _expr~2~_ \...` are evaluated in sequence and the values of the last expression are returned.

If none of the clauses contains a matching key and an `else` clause is present, the expressions `_expr~1~_ _expr~2~_ \...` of the `else` clause are evaluated in sequence and the values of the last expression are returned.

If none of the clauses contains a matching key and no `else` clause is present, the value or values are unspecified.

The Revised^6^ Report version of `case` does not support singleton keys (the second of the first two clause forms above) and uses `eqv?` rather than `equal?` as the comparison procedure. Both versions are defined in terms of `exclusive-cond` so that if profiling information is available at expansion time, the clauses will be reordered to put those that are most frequently executed first.

[source,scheme,subs="quotes"]
----
(let ([ls '(ii iv)])
  (case (car ls)
    [i 1]
    [ii 2]
    [iii 3]
    [(iiii iv) 4]
    [else 'out-of-range])) ⇒ 2

(define p
  (lambda (x)
    (case x
      [("abc" "def") 'one]
      [((a b c)) 'two]
      [else #f])))

(p (string #\d #\e #\f)) ⇒ one
(p '(a b c)) ⇒ two
----

[#control:s3]
[horizontal]
syntax:: `(record-case _expr_ _clause~1~_ _clause~2~_ \...)`
returns:: see explanation
libraries:: `(chezscheme)`

`record-case` is a restricted form of `case` that supports the destructuring of _records_, or _tagged lists_. A record has as its first element a tag that determines what "type" of record it is; the remaining elements are the fields of the record.

Each clause but the last must take the form

[source,scheme,subs="quotes"]
----
((_key_ ...) _formals_ _body~1~_ _body~2~_ ...)
----

where each `_key_` is a datum distinct from the other keys. The last clause may be in the above form or it may be an `else` clause of the form

[source,scheme,subs="quotes"]
----
(else _body~1~_ _body~2~_ ...)
----

`_expr_` must evaluate to a pair. `_expr_` is evaluated and the car of its value is compared (using `eqv?`) against the keys of each clause in order. If a clause containing a matching key is found, the variables in `_formals_` are bound to the remaining elements of the list and the expressions `_body~1~_ _body~2~_ \...` are evaluated in sequence. The value of the last expression is returned. The effect is identical to the application of

[source,scheme,subs="quotes"]
----
(lambda _formals_ _body~1~_ _body~2~_ ...)
----

to the cdr of the list.

If none of the clauses contains a matching key and an `else` clause is present, the expressions `_body~1~_ _body~2~_ \...` of the `else` clause are evaluated in sequence and the value of the last expression is returned.

If none of the clauses contains a matching key and no `else` clause is present, the value is unspecified.

[source,scheme,subs="quotes"]
----
(define calc
  (lambda (x)
    (record-case x
      [(add) (x y) (+ x y)]
      [(sub) (x y) (- x y)]
      [(mul) (x y) (* x y)]
      [(div) (x y) (/ x y)]
      [else (assertion-violationf 'calc "invalid expression ~s" x)])))

(calc '(add 3 4)) ⇒ 7
(calc '(div 3 4)) ⇒ 3/4
----

=== Section 6.2. Mapping and Folding [[section_6.2.]]

[horizontal]
procedure:: `(ormap _procedure_ _list~1~_ _list~2~_ \...)`
returns:: see explanation
libraries:: `(chezscheme)`

`ormap` is identical to the Revised^6^ Report `exists`.

[horizontal]
procedure:: `(andmap _procedure_ _list~1~_ _list~2~_ \...)`
returns:: see explanation
libraries:: `(chezscheme)`

`andmap` is identical to the Revised^6^ Report `for-all`.

=== Section 6.3. Continuations [[section_6.3.]]

_Chez Scheme_ supports one-shot continuations as well as the standard multi-shot continuations obtainable via `call/cc`. One-shot continuations are continuations that may be invoked at most once, whether explicitly or implicitly. They are obtained with `call/1cc`.

[horizontal]
procedure:: `(call/1cc _procedure_)`
returns:: see below
libraries:: `(chezscheme)`

`call/1cc` obtains its continuation and passes it to `_procedure_`, which should accept one argument. The continuation itself is represented by a procedure. This procedure normally takes one argument but may take an arbitrary number of arguments depending upon whether the context of the call to `call/1cc` expects multiple return values or not. When this procedure is applied to a value or values, it returns the values to the continuation of the `call/1cc` application.

The continuation obtained by `call/1cc` is a "one-shot continuation." A one-shot continuation should not be returned to multiple times, either by invoking the continuation or returning normally from `_procedure_` more than once. A one-shot continuation is "promoted" into a normal (multishot) continuation, however, if it is still active when a normal continuation is obtained by `call/cc`. After a one-shot continuation is promoted into a multishot continuation, it behaves exactly as if it had been obtained via `call/cc`. This allows `call/cc` and `call/1cc` to be used together transparently in many applications.

One-shot continuations may be more efficient for some applications than multishot continuations. See the paper "Representing control in the presence of one-shot continuations" <<ref3>> for more information about one-shot continuations, including how they are implemented in _Chez Scheme_.

The following examples highlight the similarities and differences between one-shot and normal continuations.

[source,scheme,subs="quotes"]
----
(define prod
 ; compute the product of the elements of ls, bugging out
 ; with no multiplications if a zero element is found
  (lambda (ls)
    (lambda (k)
      (if (null? ls)
          1
          (if (= (car ls) 0)
              (k 0)
              (* (car ls) ((prod (cdr ls)) k)))))))

(call/cc (prod '(1 2 3 4))) ⇒ 24
(call/1cc (prod '(1 2 3 4))) ⇒ 24

(call/cc (prod '(1 2 3 4 0))) ⇒ 0
(call/1cc (prod '(1 2 3 4 0))) ⇒ 0

(let ([k (call/cc (lambda (x) x))])
  (k (lambda (x) 0))) ⇒ 0

(let ([k (call/1cc (lambda (x) x))])
  (k (lambda (x) 0))) ⇒ _exception_
----

[#control:s11]
[#dynamic-wind]
[horizontal]
procedure:: `(dynamic-wind _in_ _body_ _out_)`
procedure:: `(dynamic-wind _critical?_ _in_ _body_ _out_)`
returns:: values resulting from the application of `_body_`
libraries:: `(chezscheme)`

The first form is identical to the Revised^6^ Report `dynamic-wind`. When the optional `_critical?_` argument is present and non-false, the `_in_` thunk is invoked in a critical section along with the code that records that the body has been entered, and the `_out_` thunk is invoked in a critical section along with the code that records that the body has been exited. Extreme caution must be taken with this form of `dynamic-wind`, since an error or long-running computation can leave interrupts and automatic garbage collection disabled.

=== Section 6.4. Engines [[section_6.4.]]

Engines are a high-level process abstraction supporting _timed preemption_ (<<ref15>>,<<ref24>>). Engines may be used to simulate multiprocessing, implement operating system kernels, and perform nondeterministic computations.

[horizontal]
procedure:: `(make-engine _thunk_)`
returns:: an engine
libraries:: `(chezscheme)`

An engine is created by passing a thunk (no argument procedure) to `make-engine`. The body of the thunk is the computation to be performed by the engine. An engine itself is a procedure of three arguments:

`_ticks_`::
    a positive integer that specifies the amount of _fuel_ to be given to the engine. An engine executes until this fuel runs out or until its computation finishes.

`_complete_`::
    a procedure of one or more arguments that specifies what to do if the computation finishes. Its arguments are the amount of fuel left over and the values produced by the computation.

`_expire_`::
    a procedure of one argument that specifies what to do if the fuel runs out before the computation finishes. Its argument is a new engine capable of continuing the computation from the point of interruption.

When an engine is applied to its arguments, it sets up a timer to fire in `_ticks_` time units. (See `set-timer` on <<desc:set-timer,page 330>>.) If the engine computation completes before the timer expires, the system invokes `_complete_`, passing it the number of `_ticks_` left over and the values produced by the computation. If, on the other hand, the timer goes off before the engine computation completes, the system creates a new engine from the continuation of the interrupted computation and passes this engine to `_expire_`. `_complete_` and `_expire_` are invoked in the continuation of the engine invocation.

An implementation of engines is given in link:../../the-scheme-programming-language-4th/en/index.html#section_12.11.[Section 12.11] of _The Scheme Programming Language, 4th Edition_.

Do not use the timer interrupt (see `set-timer`) and engines at the same time, since engines are implemented in terms of the timer.

The following example creates an engine from a trivial computation, 3, and gives the engine 10 ticks.

[source,scheme,subs="quotes"]
----
(define eng
  (make-engine
    (lambda () 3)))

(eng 10
     (lambda (ticks value) value)
     (lambda (x) x)) ⇒ 3
----

It is often useful to pass `list` as the `_complete_` procedure to an engine, causing an engine that completes to return a list whose first element is the ticks remaining and whose remaining elements are the values returned by the computation.

[source,scheme,subs="quotes"]
----
(define eng
  (make-engine
    (lambda () 3)))

(eng 10
     list
     (lambda (x) x)) ⇒ (9 3)
----

In the example above, the value is 3 and there are 9 ticks left over, i.e., it takes one unit of fuel to evaluate 3. (The fuel amounts given here are for illustration only. Your mileage may vary.)

Typically, the engine computation does not finish in one try. The following example displays the use of an engine to compute the 10th Fibonacci number in steps.

[source,scheme,subs="quotes"]
----
(define fibonacci
  (lambda (n)
    (let fib ([i n])
      (cond
        [(= i 0) 0]
        [(= i 1) 1]
        [else (+ (fib (- i 1))
                 (fib (- i 2)))]))))

(define eng
  (make-engine
    (lambda ()
      (fibonacci 10))))

(eng 50
     list
     (lambda (new-eng)
       (set! eng new-eng)
       "expired")) ⇒ "expired"

(eng 50
     list
     (lambda (new-eng)
       (set! eng new-eng)
       "expired")) ⇒ "expired"

(eng 50
     list
     (lambda (new-eng)
       (set! eng new-eng)
       "expired")) ⇒ "expired"

(eng 50
     list
     (lambda (new-eng)
       (set! eng new-eng)
       "expired")) ⇒ (21 55)
----

Each time the engine's fuel runs out, the `_expire_` procedure assigns `eng` to the new engine. The entire computation requires four blocks of 50 ticks to complete; of the last 50 it uses all but 21. Thus, the total amount of fuel used is 179 ticks. This leads to the following procedure, `mileage`, which "times" a computation using engines:

[source,scheme,subs="quotes"]
----
(define mileage
  (lambda (thunk)
    (let loop ([eng (make-engine thunk)] [total-ticks 0])
      (eng 50
           (lambda (ticks . values)
             (+ total-ticks (- 50 ticks)))
           (lambda (new-eng)
             (loop new-eng
                   (+ total-ticks 50)))))))

(mileage (lambda () (fibonacci 10))) ⇒ 179
----

The choice of 50 for the number of ticks to use each time is arbitrary, of course. It might make more sense to pass a much larger number, say 10000, in order to reduce the number of times the computation is interrupted.

The next procedure is similar to `mileage`, but it returns a list of engines, one for each tick it takes to complete the computation. Each of the engines in the list represents a "snapshot" of the computation, analogous to a single frame of a moving picture. `snapshot` might be useful for "single stepping" a computation.

[source,scheme,subs="quotes"]
----
(define snapshot
  (lambda (thunk)
    (let again ([eng (make-engine thunk)])
      (cons eng
            (eng 1 (lambda (t . v) '()) again)))))
----

The recursion embedded in this procedure is rather strange. The complete procedure performs the base case, returning the empty list, and the expire procedure performs the recursion.

The next procedure, `round-robin`, could be the basis for a simple time-sharing operating system. `round-robin` maintains a queue of processes (a list of engines), cycling through the queue in a _round-robin_ fashion, allowing each process to run for a set amount of time. `round-robin` returns a list of the values returned by the engine computations in the order that the computations complete. Each computation is assumed to produce exactly one value.

[source,scheme,subs="quotes"]
----
(define round-robin
  (lambda (engs)
    (if (null? engs)
        '()
        ((car engs)
         1
         (lambda (ticks value)
           (cons value (round-robin (cdr engs))))
         (lambda (eng)
           (round-robin
             (append (cdr engs) (list eng))))))))
----

Since the amount of fuel supplied each time, one tick, is constant, the effect of `round-robin` is to return a list of the values sorted from the quickest to complete to the slowest to complete. Thus, when we call `round-robin` on a list of engines, each computing one of the Fibonacci numbers, the output list is sorted with the earlier Fibonacci numbers first, regardless of the order of the input list.

[source,scheme,subs="quotes"]
----
(round-robin
  (map (lambda (x)
         (make-engine
           (lambda ()
             (fibonacci x))))
       '(4 5 2 8 3 7 6 2))) ⇒ (1 1 2 3 5 8 13 21)
----

More interesting things can happen if the amount of fuel varies each time through the loop. In this case, the computation would be nondeterministic, i.e., the results would vary from call to call.

The following syntactic form, `por` (parallel-or), returns the first of its expressions to complete with a true value. `por` is implemented with the procedure `first-true`, which is similar to `round-robin` but quits when any of the engines completes with a true value. If all of the engines complete, but none with a true value, `first-true` (and hence `por`) returns `#f`. Also, although `first-true` passes a fixed amount of fuel to each engine, it chooses the next engine to run at random, and is thus nondeterministic.

[source,scheme,subs="quotes"]
----
(define-syntax por
  (syntax-rules ()
    [(_ x ...)
     (first-true
       (list (make-engine (lambda () x)) ...))]))

(define first-true
  (let ([pick
         (lambda (ls)
           (list-ref ls (random (length ls))))])
    (lambda (engs)
      (if (null? engs)
          #f
          (let ([eng (pick engs)])
            (eng 1
                 (lambda (ticks value)
                   (or value
                       (first-true
                         (remq eng engs))))
                 (lambda (new-eng)
                   (first-true
                     (cons new-eng
                           (remq eng engs))))))))))
----

The list of engines is maintained with `pick`, which randomly chooses an element of the list, and `remq`, which removes the chosen engine from the list. Since `por` is nondeterministic, subsequent uses with the same expressions may not return the same values.

[source,scheme,subs="quotes"]
----
(por 1 2 3) ⇒ 2
(por 1 2 3) ⇒ 3
(por 1 2 3) ⇒ 2
(por 1 2 3) ⇒ 1
----

Furthermore, even if one of the expressions is an infinite loop, `por` still finishes as long as one of the other expressions completes and returns a true value.

[source,scheme,subs="quotes"]
----
(por (let loop () (loop)) 2) ⇒ 2
----

With `engine-return` and `engine-block`, it is possible to terminate an engine explicitly. `engine-return` causes the engine to complete, as if the computation had finished. Its arguments are passed to the `_complete_` procedure along with the number of ticks remaining. It is essentially a nonlocal exit from the engine. Similarly, `engine-block` causes the engine to expire, as if the timer had run out. A new engine is made from the continuation of the call to `engine-block` and passed to the `_expire_` procedure.

[horizontal]
procedure:: `(engine-block)`
returns:: does not return
libraries:: `(chezscheme)`

This causes a running engine to stop, create a new engine capable of continuing the computation, and pass the new engine to the original engine's third argument (the expire procedure). Any remaining fuel is forfeited.

[source,scheme,subs="quotes"]
----
(define eng
  (make-engine
    (lambda ()
      (engine-block)
      "completed")))

(eng 100
     (lambda (ticks value) value)
     (lambda (x)
        (set! eng x)
        "expired")) ⇒ "expired"

(eng 100
     (lambda (ticks value) value)
     (lambda (x)
        (set! eng x)
        "expired")) ⇒ "completed"
----

[#control:s28]
[horizontal]
procedure:: `(engine-return _obj_ \...)`
returns:: does not return
libraries:: `(chezscheme)`

This causes a running engine to stop and pass control to the engine's `_complete_` argument. The first argument passed to the complete procedure is the amount of fuel remaining, as usual, and the remaining arguments are the objects `_obj_ \...` passed to `engine-return`.

[source,scheme,subs="quotes"]
----
(define eng
  (make-engine
    (lambda ()
      (reverse (engine-return 'a 'b 'c)))))

(eng 100
     (lambda (ticks . values) values)
     (lambda (new-eng) "expired")) ⇒ (a b c)
----
