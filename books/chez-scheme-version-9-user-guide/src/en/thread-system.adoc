[#chp_15]
== Chapter 15. Thread System

This chapter describes the _Chez Scheme_ thread-system procedures and syntactic forms. With the exception of locks, locked increment, and locked decrement, the features of the thread system are implemented on top of the Posix thread system (pthreads) on non-Windows-based system and directly using the Windows API on Windows-based systems. Consult the appropriate documentation on your system for basic details of thread creation and interaction.

Most primitive Scheme procedures are _thread-safe_, meaning that they can be called concurrently from multiple threads. This includes allocation operations like `_cons_` and `make-string`, accessors like `car` and `vector-ref`, numeric operators like `+` and `sqrt`, and nondestructive higher-level primitive operators like `append` and `map`.

Simple mutation operators, like `set-car!`, `vector-set!`, and record field mutators are thread-safe. Likewise, assignments to local variables, including assignments to (unexported) library and top-level program variables are thread-safe.

Other destructive operators are thread safe only if they are used to operate on different objects from those being read or modified by other threads. For example, assignments to global variables are thread-safe only as long as one thread does not assign the same variable another thread references or assigns. Similarly, `putprop` can be called in one thread while another concurrently calls `putprop` or `getprop` if the symbols whose property lists are being modified or accessed differ.

In this context, most I/O operations should be considered destructive, since they might modify a port's internal structure; see also <<section_15.8.,Section 15.8>> for information on buffered ports.

Use of operators that are not thread-safe without proper synchronization can corrupt the objects upon which they operate. This corruption can lead to incorrect behavior, memory faults, and even unrecoverable errors that cause the system to abort.

The compiler and interpreter are thread-safe to the extent that user code evaluated during the compilation and evaluation process is thread-safe or properly synchronized. Thus, two or more threads can call any of the compiler or interpreter entry points, i.e., `compile`, `compile-file`, `compile-program`, `compile-script`, `compile-port`, or `interpret` at the same time. Naturally, the object-file targets of two file compilation operations that run at the same time should be different. The same is true for `eval` and `load` as long as the default evaluator is used or is set explicitly to `compile`, `interpret`, or some other thread-safe evaluator.

One restriction should be observed when one of multiple threads creates or loads compiled code, however, which is that only that thread or subsequently created children, or children of subsequently created children, etc., should run the code. This is because multiple-processor systems upon which threaded code may run might not guarantee that the data and instruction caches are synchronized across processors.

=== Section 15.1. Thread Creation [[section_15.1.]]

[horizontal]
procedure:: `(fork-thread _thunk_)`
returns:: a thread object
libraries:: `(chezscheme)`

`_thunk_` must be a procedure that accepts zero arguments.

`fork-thread` invokes `_thunk_` in a new thread and returns a thread object.

Nothing can be done with the thread object returned by `fork-thread`, other than to print it.

Threads created by foreign code using some means other than `fork-thread` must call `Sactivate_thread` (<<section_4.8.,Section 4.8>>) before touching any Scheme data or calling any Scheme procedures.

[horizontal]
procedure:: `(thread? _obj_)`
returns:: `#t` if `_obj_` is a thread object, `#f` otherwise
libraries:: `(chezscheme)`

[#threads:s4]
[horizontal]
procedure:: `(get-thread-id)`
returns:: the thread id of the current thread
libraries:: `(chezscheme)`

The thread id is a thread number assigned by thread id, and has no relationship to the process id returned by `get-process-id`, which is the same in all threads.

=== Section 15.2. Mutexes [[section_15.2.]]

[horizontal]
procedure:: `(make-mutex)`
procedure:: `(make-mutex _name_)`
returns:: a new mutex object
libraries:: `(chezscheme)`

`_name_`, if supplied, must be a symbol which identifies the mutex, or `#f` for no name. The name is printed every time the mutex is printed, which is useful for debugging.

[horizontal]
procedure:: `(mutex? _obj_)`
returns:: `#t` if `_obj_` is a mutex, `#f` otherwise
libraries:: `(chezscheme)`

[#threads:s8]
[horizontal]
procedure:: `(mutex-acquire _mutex_)`
procedure:: `(mutex-acquire _mutex_ _block?_)`
returns:: see below
libraries:: `(chezscheme)`

`_mutex_` must be a mutex.

`_mutex-acquire_` acquires the mutex identified by `_mutex_`. The optional boolean argument `_block?_` defaults to `#t` and specifies whether the thread should block waiting for the mutex. If `_block?_` is omitted or is true, the thread blocks until the mutex has been acquired, and an unspecified value is returned.

If `block?` is false and the mutex currently belongs to a different thread, the current thread does not block. Instead, `mutex-acquire` returns immediately with the value `#f` to indicate that the mutex is not available. If `_block?_` is false and the mutex is successfully acquired, `mutex-acquire` returns `#t`.

Mutexes are _recursive_ in Posix threads terminology, which means that the calling thread can use `mutex-acquire` to (re)acquire a mutex it already has. In this case, an equal number of `mutex-release` calls is necessary to release the mutex.

[horizontal]
procedure:: `(mutex-release _mutex_)`
returns:: unspecified
libraries:: `(chezscheme)`

`_mutex_` must be a mutex.

`mutex-release` releases the mutex identified by `_mutex_`. Unpredictable behavior results if the mutex is not owned by the calling thread.

[horizontal]
syntax:: `(with-mutex _mutex_ _body~1~_ _body~2~_ \...)`
returns:: the values of the body `_body~1~_ _body~2~_ \...`
libraries:: `(chezscheme)`

`with-mutex` evaluates the expression `_mutex_`, which must evaluate to a mutex, acquires the mutex, evaluates the body `_body~1~_ _body~2~_ \...`, and releases the mutex. The mutex is released whether the body returns normally or via a control operation (that is, throw to a continuation, perhaps because of an error) that results in a nonlocal exit from the `with-mutex` form. If control subsequently returns to the body via a continuation invocation, the mutex is reacquired.

Using `with-mutex` is generally more convenient and safer than using `mutex-acquire` and `mutex-release` directly.

[horizontal]
procedure:: `(mutex-name _mutex_)`
returns:: the name associated with `_mutex_`, if any; otherwise `#f`
libraries:: `(chezscheme)`

`_mutex_` must be a mutex.

=== Section 15.3. Conditions [[section_15.3.]]

[horizontal]
procedure:: `(make-condition)`
procedure:: `(make-condition _name_)`
returns:: a new condition object
libraries:: `(chezscheme)`

`_name_`, if supplied, must be a symbol which identifies the condition object, or `#f` for no name. The name is printed every time the condition is printed, which is useful for debugging.

[horizontal]
procedure:: `(thread-condition? _obj_)`
returns:: `#t` if `_obj_` is a condition object, `#f` otherwise
libraries:: `(chezscheme)`

[#threads:s14]
[horizontal]
procedure:: `(condition-wait _cond_ _mutex_)`
procedure:: `(condition-wait _cond_ _mutex_ _timeout_)`
returns:: `#t` if the calling thread was awakened by the condition, `#f` if the calling thread timed out waiting
libraries:: `(chezscheme)`

`_cond_` must be a condition object, and `_mutex_` must be a mutex. The optional argument `_timeout_` is a time record of type `time-duration` or `time-utc`, or `#f` for no timeout. It defaults to `#f`.

`condition-wait` waits up to the specified `_timeout_` for the condition identified by the condition object `_cond_`. The calling thread must have acquired the mutex identified by the mutex `_mutex_` at the time `condition-wait` is called. `_mutex_` is released as a side effect of the call to `condition-wait`. When a thread is later released from the condition variable by one of the procedures described below or the timeout expires, `_mutex_` is reacquired and `condition-wait` returns.

[horizontal]
procedure:: `(condition-signal _cond_)`
returns:: unspecified
libraries:: `(chezscheme)`

`_cond_` must be a condition object.

`condition-signal` releases one of the threads waiting for the condition identified by `_cond_`.

[horizontal]
procedure:: `(condition-broadcast _cond_)`
returns:: unspecified
libraries:: `(chezscheme)`

`_cond_` must be a condition object.

`condition-broadcast` releases all of the threads waiting for the condition identified by `_cond_`.

[horizontal]
procedure:: `(condition-name _condition_)`
returns:: the name associated with `_condition_`, if any; otherwise `#f`
libraries:: `(chezscheme)`

`_condition_` must be a condition.

=== Section 15.4. Locks [[section_15.4.]]

Locks are more primitive but more flexible and efficient than mutexes and can be used in situations where the added mutex functionality is not needed or desired. They can also be used independently of the thread system (including in nonthreaded versions of _Chez Scheme_) to synchronize operations running in separate Scheme processes as long as the lock is allocated in memory shared by the processes.

A lock is simply a word-sized integer, i.e., an `iptr` or `uptr` foreign type (<<section_4.5.,Section 4.5>>) with the native endiannes of the target machine, possibly part of a larger structure defined using `define-ftype` (<<defn:define-ftype,page 77>>). It must be explicitly allocated in memory that resides outside the Scheme heap and, when appropriate, explicitly deallocated. When just threads are involved (i.e., when multiple processes are not involved), the memory can be allocated via `foreign-alloc`. When multiple processes are involved, the lock should be allocated in some area shared by the processes that will interact with the lock.

Once initialized using `ftype-init-lock!`, a process or thread can attempt to lock the lock via `ftype-lock!` or `ftype-spin-lock!`. Once the lock has been locked and before it is unlocked, further attempts to lock the lock fail, even by the process or thread that most recently locked it. Locks can be unlocked, via `ftype-unlock!`, by any process or thread, not just by the process or thread that most recently locked the lock.

The lock mechanism provides little structure, and mistakes in allocation and use can lead to memory faults, deadlocks, and other problems. Thus, it is usually advisable to use locks only as part of a higher-level abstraction that ensures locks are used in a disciplined manner.

[source,scheme,subs="quotes"]
----
(define lock
  (make-ftype-pointer uptr
    (foreign-alloc (ftype-sizeof uptr))))

(ftype-init-lock! uptr () lock)
(ftype-lock! uptr () lock) ⇒ #t
(ftype-lock! uptr () lock) ⇒ #f
(ftype-unlock! uptr () lock)
(ftype-spin-lock! uptr () lock)
(ftype-lock! uptr () lock) ⇒ #f
(ftype-unlock! uptr () lock)
----

[#threads:s19]
[horizontal]
syntax:: `(ftype-init-lock! _ftype-name_ (_a_ \...) _fptr-expr_)`
syntax:: `(ftype-init-lock! _ftype-name_ (_a_ \...) _fptr-expr_ _index_)`
returns:: unspecified
syntax:: `(ftype-lock! _ftype-name_ (_a_ \...) _fptr-expr_)`
syntax:: `(ftype-lock! _ftype-name_ (_a_ \...) _fptr-expr_ _index_)`
returns:: `#t` if the lock is not already locked, `#f` otherwise
syntax:: `(ftype-spin-lock! _ftype-name_ (_a_ \...) _fptr-expr_)`
syntax:: `(ftype-spin-lock! _ftype-name_ (_a_ \...) _fptr-expr_ _index_)`
returns:: unspecified
syntax:: `(ftype-unlock! _ftype-name_ (_a_ \...) _fptr-expr_)`
syntax:: `(ftype-unlock! _ftype-name_ (_a_ \...) _fptr-expr_ _index_)`
returns:: unspecified
libraries:: `(chezscheme)`

Each of these has a syntax like and behaves similarly to `ftype-set!` (<<defn:ftype-set!,page 86>>), though with an implicit `_val-expr_`. In particular, the restrictions on and handling of `_fptr-expr_` and the accessors `_a_ \...` is similar, with one important restriction: the field specified by the last accessor, upon which the form operates, must be a word-size integer, i.e., an `iptr`, `uptr`, or the equivalent, with the native endianness.

`ftype-init-lock!` should be used to initialize the lock prior to the use of any of the other operators; if this is not done, the behavior of the other operators is undefined.

`ftype-lock!` can be used to lock the lock. If it finds the lock unlocked at the time of the operation, it locks the lock and returns `#t`; if it finds the lock already locked, it returns `#f` without changing the lock.

`ftype-spin-lock!` can also be used to lock the lock. If it finds the lock unlocked at the time of the operation, it locks the lock and returns; if it finds the lock already locked, it waits until the lock is unlocked, then locks the lock and returns. If no other thread or process unlocks the lock, the operation does not return and cannot be interrupted by normal means, including by the storage manager for the purpose of initiating a garbage collection. There are also no guarantees of fairness, so a process might hang indefinitely even if other processes are actively locking and unlocking the lock.

`ftype-unlock!` is used to unlock a lock. If it finds the lock locked, it unlocks the lock and returns. Otherwise, it returns without changing the lock.

=== Section 15.5. Locked increment and decrement [[section_15.5.]]

The locked operations described here can be used when just an atomic increment or decrement is required.

[horizontal]
syntax:: `(ftype-locked-incr! _ftype-name_ (_a_ \...) _fptr-expr_)`
syntax:: `(ftype-locked-incr! _ftype-name_ (_a_ \...) _fptr-expr_ _index_)`
returns:: `#t` if the updated value is 0, `#f` otherwise
syntax:: `(ftype-locked-decr! _ftype-name_ (_a_ \...) _fptr-expr_)`
syntax:: `(ftype-locked-decr! _ftype-name_ (_a_ \...) _fptr-expr_ _index_)`
returns:: `#t` if the updated value is 0, `#f` otherwise
libraries:: `(chezscheme)`

Each of these has a syntax like and behaves similarly to `ftype-set!` (<<defn:ftype-set!,page 86>>), though with an implicit `_val-expr_`. In particular, the restrictions on and handling of `_fptr-expr_` and the accessors `_a_ \...` is similar, with one important restriction: the field specified by the last accessor, upon which the form operates, must be a word-size integer, i.e., an `iptr`, `uptr`, or the equivalent, with the native endianness.

`ftype-locked-incr!` atomically reads the value of the specified field, adds 1 to the value, and writes the new value back into the field. Similarly, `ftype-locked-decr!` atomically reads the value of the specified field, subtracts 1 from the value, and writes the new value back into the field. Both return `#t` if the new value is 0, otherwise `#f`.

=== Section 15.6. Reference counting with ftype guardians [[section_15.6.]]

Applications that manage memory outside the Scheme heap can leverage the Scheme storage management system to help perform reference counting via _ftype guardians_. In a reference-counted memory management system, each object holds a count of pointers to it. The count is incremented when a new pointer is created and decremented when a pointer is dropped. When the count reaches zero, the object is no longer needed and the memory it formerly occupied can be made available for some other purpose.

Ftype guardians are similar to guardians created by `make-guardian` (<<section_13.2.,Section 13.2>>). The `guardian?` procedure returns true for both, and the `unregister-guardian` procedure can be used to unregister objects registered with either.

[horizontal]
syntax:: `(ftype-guardian _ftype-name_)`
returns:: a new ftype guardian
libraries:: `(chezscheme)`

`_ftype-name_` must name an ftype. The first base field of the ftype (or one of the first base fields in the case of unions) must be a word-sized integer (iptr or uptr) with native endianness. This field is assumed to hold a reference count.

The return value is a new ftype guardian `_g_`, with which ftype-pointers of type `_ftype-name_` (or some subtype of `_ftype-name_`) can be registered. An ftype pointer is registered with `_g_` by invoking `_g_` with the ftype pointer as an argument.

An ftype guardian does not automatically protect from collection the ftype pointers registered with it, as a normal guardian would do. Instead, for each registered ftype pointer that becomes inaccessible via normal (non-weak, non-guardian pointers), the guardian decrements the reference count of the object to which the ftype pointer points. If the resulting reference-count value is zero, the ftype pointer is preserved and can be retrieved from the guardian. If the resulting reference-count value is non-zero, however, the ftype pointer is not preserved. Objects retrieved from an ftype guardian (by calling it without arguments) are guaranteed to have zero reference counts, assuming reference counts are maintained properly by code outside the collector.

The collector decrements the reference count using the equivalent of `ftype-locked-decr!` to support systems in which non-Scheme objects are stored in memory shared by multiple processes. In such systems, programs should themselves use `ftype-locked-incr!` and `ftype-locked-decr!` or non-Scheme equivalents (e.g., the C `LOCKED_INCR` and `LOCKED_DECR` macros in scheme.h, which are described in <<section_4.8.,Section 4.8>>) to maintain reference counts.

The following example defines a simple ftype and an allocator for objects of that ftype that frees any objects of that ftype that were previously allocated and no longer accessible.

[source,scheme,subs="quotes"]
----
(module (A make-A free-dropped-As)
  (define-ftype A
    (struct
      [refcount uptr]
      [data int]))
  (define g (ftype-guardian A))
  (define free-dropped-As
    (lambda ()
      (let ([a (g)])
        (when a
          (printf "freeing ~s\n" (ftype-ref A (data) a))
          (foreign-free (ftype-pointer-address a))
          (free-dropped-As)))))
  (define make-A
    (lambda (n)
      (free-dropped-As)
      (let ([a (make-ftype-pointer A (foreign-alloc (ftype-sizeof A)))])
        (ftype-set! A (refcount) a 1)
        (ftype-set! A (data) a n)
        (g a)
        a))))
----

We can test this by allocating, dropping, and immediately collecting ftype pointers to A.

[source,scheme,subs="quotes"]
----
> (do ([i 10 (fx- i 1)])
      ((fx= i 0))
    (make-A i)
    (collect))
freeing 10
freeing 9
freeing 8
freeing 7
freeing 6
freeing 5
freeing 4
freeing 3
freeing 2
> (free-dropped-As)
freeing 1
----

Objects guarded by an ftype guardian might contain pointers to other objects whose reference counts should also be incremented upon allocation of the containing object and decremented upon freeing of the containing object.

=== Section 15.7. Thread Parameters [[section_15.7.]]

[horizontal]
procedure:: `(make-thread-parameter _object_)`
procedure:: `(make-thread-parameter _object_ _procedure_)`
returns:: a new thread parameter
libraries:: `(chezscheme)`

See <<section_12.13.,Section 12.13>> for a general discussion of parameters and the use of the optional second argument.

When a thread parameter is created, a separate location is set aside in each current and future thread to hold the value of the parameter's internal state variable. (This location may be eliminated by the storage manager when the parameter becomes inaccessible.) Changes to the thread parameter in one thread are not seen by any other thread.

When a new thread is created (see `fork-thread`), the current value (not location) of each thread parameter is inherited from the forking thread by the new thread. Similarly, when a thread created by some other means is activated for the first time (see `Sactivate_thread` in <<section_4.8.,Section 4.8>>), the current value (not location) of each thread parameter is inherited from the main (original) thread by the new thread.

Most built-in parameters are thread parameters, but some are global. All are marked as global or thread where they are defined. There is no distinction between built-in global and thread parameters in the nonthreaded versions of the system.

=== Section 15.8. Buffered I/O [[section_15.8.]]

Chez Scheme buffers file I/O operations for efficiency, but buffered I/O is not thread safe. Two threads that write to or read from the same buffered port concurrently can corrupt the port, resulting in buffer overruns and, ultimately, invalid memory references.

Buffering on binary output ports can be disabled when opened with buffer-mode `none`. Buffering on input ports cannot be completely disabled, however, due to the need to support lookahead, and buffering on textual ports, even textual output ports, cannot be disabled completely because the transcoders that convert between characters and bytes sometimes require some lookahead.

Two threads should thus _never_ read from or write to the same port concurrently, except in the special case of a binary output port opened buffer-mode `none`. Alternatives include appointing one thread to perform all I/O for a given port and providing a per-thread generic-port wrapper that forwards requests to the port only after acquiring a mutex.

The initial console and current input and output ports are thread-safe, as are transcript ports, so it is safe for multiple threads to print error and/or debugging messages to the console. The output may be interleaved, even within the same line, but the port will not become corrupted. Thread safety for these ports is accomplished at the high cost of acquiring a mutex for each I/O operation.

=== Section 15.9. Example: Bounded Queues [[section_15.9.]]

The following code, taken from the article "A Scheme for native threads <<ref10>>," implements a bounded queue using many of the thread-system features. A bounded queue has a fixed number of available slots. Attempting to enqueue when the queue is full causes the calling thread to block. Attempting to dequeue from an empty queue causes the calling thread to block.

[source,scheme,subs="quotes"]
----
(define-record-type bq
  (fields
    (immutable data)
    (mutable head)
    (mutable tail)
    (immutable mutex)
    (immutable ready)
    (immutable room))
  (protocol
    (lambda (new)
      (lambda (bound)
        (new (make-vector bound) 0 0 (make-mutex)
          (make-condition) (make-condition))))))

(define dequeue!
  (lambda (q)
    (with-mutex (bq-mutex q)
      (let loop ()
        (let ([head (bq-head q)])
          (cond
            [(= head (bq-tail q))
             (condition-wait (bq-ready q) (bq-mutex q))
             (loop)]
            [else
             (bq-head-set! q (incr q head))
             (condition-signal (bq-room q))
             (vector-ref (bq-data q) head)]))))))

(define enqueue!
  (lambda (item q)
    (with-mutex (bq-mutex q)
      (let loop ()
        (let* ([tail (bq-tail q)] [tail^ (incr q tail)])
          (cond
            [(= tail^ (bq-head q))
             (condition-wait (bq-room q) (bq-mutex q))
             (loop)]
            [else
             (vector-set! (bq-data q) tail item)
             (bq-tail-set! q tail^)
             (condition-signal (bq-ready q))]))))))

(define incr
  (lambda (q i)
    (modulo (+ i 1) (vector-length (bq-data q)))))
----

The code below demonstrates the use of the bounded queue abstraction with a set of threads that act as consumers and producers of the data in the queue.

[source,scheme,subs="quotes"]
----
(define job-queue)
(define die? #f)

(define make-job
  (let ([count 0])
    (define fib
      (lambda (n)
        (if (< n 2)
            n
            (+ (fib (- n 2)) (fib (- n 1))))))
    (lambda (n)
      (set! count (+ count 1))
      (printf "Adding job #~s = (lambda () (fib ~s))\n" count n)
      (cons count (lambda () (fib n))))))

(define make-producer
  (lambda (n)
    (rec producer
      (lambda ()
        (printf "producer ~s posting a job\n" n)
        (enqueue! (make-job (+ 20 (random 10))) job-queue)
        (if die?
            (printf "producer ~s dying\n" n)
            (producer))))))

(define make-consumer
  (lambda (n)
    (rec consumer
      (lambda ()
        (printf "consumer ~s looking for a job~%" n)
        (let ([job (dequeue! job-queue)])
          (if die?
              (printf "consumer ~s dying\n" n)
              (begin
                (printf "consumer ~s executing job #\~s~%" n (car job))
                (printf "consumer ~s computed:  \~s~%" n ((cdr job)))
                (consumer))))))))

(define (bq-test np nc)
  (set! job-queue (make-bq (max nc np)))
  (do ([np np (- np 1)])
      ((<= np 0))
      (fork-thread (make-producer np)))
  (do ([nc nc (- nc 1)])
      ((<= nc 0))
      (fork-thread (make-consumer nc))))
----

Here are a possible first several lines of output from a sample run of the example program.

[source,scheme,subs="quotes"]
----
> (begin
    (bq-test 3 4)
    (system "sleep 3")
    (set! die? #t))
producer 3 posting a job
Adding job #1 = (lambda () (fib 29))
producer 3 posting a job
Adding job #2 = (lambda () (fib 26))
producer 3 posting a job
Adding job #3 = (lambda () (fib 22))
producer 3 posting a job
Adding job #4 = (lambda () (fib 21))
producer 2 posting a job
Adding job #5 = (lambda () (fib 29))
producer 1 posting a job
Adding job #6 = (lambda () (fib 29))
consumer 4 looking for a job
producer 3 posting a job
Adding job #7 = (lambda () (fib 24))
consumer 4 executing job #1
consumer 3 looking for a job
producer 2 posting a job
Adding job #8 = (lambda () (fib 26))
consumer 3 executing job #2
consumer 3 computed:  121393
consumer 3 looking for a job
producer 1 posting a job
Adding job #9 = (lambda () (fib 26))
...
----

Additional examples, including definitions of suspendable threads and threads that automatically terminate when they become inaccessible, are given in "A Scheme for native threads <<ref10>>."
